plugins {
    id 'java'
    id 'idea'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'me.byteful.plugin.tradeconomy'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'lucko'
        url = "https://repo.lucko.me/"
    }
    maven {
        name = 'redempt'
        url = 'https://redempt.dev'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation 'me.lucko:helper:5.6.9'
    implementation 'com.github.Redempt:RedCommands:1.5.7'

    // DataProvider dependencies
    implementation 'org.mongodb:mongo-java-driver:3.12.10'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('*.yml') {
        expand props
    }
}

shadowJar {
    minimize()

    archiveBaseName.set("Tradeconomy")
    archiveClassifier.set("")

    relocate 'me.lucko', 'me.byteful.plugin.tradeconomy.libs.lucko'
}